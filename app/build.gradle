apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: "$project.rootDir/secretconfig.gradle"

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        applicationId "com.lounah.tinkoffnews"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        versionName "2.0.0 (No libs) " + getDate()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "DB_NAME", "\"tinkoff_news.db\"")
        buildConfigField("String", "BASE_URL", "\"https://api.tinkoff.ru/\"")
    }

    signingConfigs {
        config {
            keyAlias signConfigs.keyAlias
            keyPassword signConfigs.keyPassword
            storeFile file(signConfigs.storeFile)
            storePassword signConfigs.storePassword
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            multiDexEnabled true
            minifyEnabled false
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    androidExtensions {
        experimental = true
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "armeabi-v7a"
            universalApk true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    // //// BASE DEPENDENCIES
    // ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    implementation deps.android_ktx
    implementation deps.support.app_compat
    implementation deps.support.recycler
    implementation deps.support.design
    implementation deps.timber
    implementation deps.licenses_dialog
    implementation deps.gson

    implementation 'com.android.support:multidex:1.0.3'

    // ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    // //// REACTIVE
    // ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    implementation deps.rx.rxjava
    implementation deps.rx.rxandroid


    // ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    // //// ARCH
    // ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    implementation deps.dagger.runtime
    implementation deps.dagger.support
    kapt deps.dagger.processor
    kapt deps.dagger.compiler

    // ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    // //// TESTING
    // ///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
    testImplementation deps.tests.junit
    testImplementation deps.tests.mockito
    testImplementation deps.mocks_maker

    androidTestImplementation deps.tests.androidTestRules
}

static def getDate() {
    return new Date().format('yyyy/MM/dd')
}